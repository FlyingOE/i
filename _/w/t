/n:{x<100};n{x*2}/1 /128
1    /1
12   /12
-234 /-234
1.2 /1.2
-2. /-2.0
-1 -2 -3 /-1 -2 -3
1+2  /3
1-2-3 /2
1-2-3-4 /-2
,1 2 /,1 2
!4   /0 1 2 3
-(1;2 3) /(-1;-2 -3)
-'(1;2 3) /(-1;-2 -3)
-'`a`b!(1;2) /`a`b!(-1;-2)
-`a`b!(1;2) /`a`b!(-1;-2)
(1;2 3)+4 /(5;6 7)
6-(1;2 3) /(5;4 3)
4+(1;2 3) /(5;6 7)
10 20+'(1 2;3 4) /(11 12;23 24)
-2>-!5 /0 0 0 1 1
1 -2>(!5;-!5) /(1 0 0 0 0;0 0 0 1 1)
2 3 4&4 3 2 /2 3 2
3&4 3 2 /3 3 2
4 3 2&3 /3 3 2
4 3 2&(2.0;(3 4);5) /(2.0;3 3;2)
1_1 2 /2
1_(1;2.0) /,2.0
"abc","d" /"abcd"
`abc /`abc
`"abc" /`abc
`a``b /`a``b
#``"ab"`cd /3
1 2,(3 4;5 6) /(1;2;3 4;5 6)
(3 4;5 6),1 2 /(3 4;5 6;1;2)
+/!4 /6
+/1 2 3 /6
-/3 5 6 /-8
+\1 2 3 /(1;3;6)
,/(1;2;3 4 5;6 7) /1 2 3 4 5 6 7
,//(1 2;(3 4 5;6 7)) /1 2 3 4 5 6 7
,//(1;(2 3);(4;(5 6))) /1 2 3 4 5 6
,/\(1;(2 3;(4 5 6))) /((1;(2 3;4 5 6));(1;2 3;4 5 6);1 2 3 4 5 6)
1 2 3[2] /3
(1;2;3) /(1;2;3)
(1;2+3) /(1;5)
`a`b!1 2 /`a`b!(1;2)
1;2 /2
1 +  2 3 /3 4
*(`a;`b) /`a
{x+y} /{x+y}
{[a;b]a+b} /{[a;b]a+b}
3{x+y}4 /7
{x+y}[4;5] /9
{x+y+z}[1;2;3] /6
{x+y}\1 2 3 /(1;3;6)
10 20+/`a`b!(1 2;3 4) /`a`b!(11 22;13 24)
(`a`b!(1 2 3;4 5 6))+\10 20 30 /`a`b!(11 22 33;14 25 36)
1_`a /0#`
2#3.0 /3 3.0
2#,3.0 /(3.0;3.0)
&"1" /1
&"-" /-
&"+:" /+:
&"1;2" /(;1;2)
&"1+2" /(+;1;2)
&"1-2-3" /(-;1;(-;2;3))
&"a:1" /(:;`a;1)
&"a::1" /(::;`a;1)
&"a[3]:1" /(:;(`a;3);1)
&"a[;2]:1" /(:;(`a;;2);1)
&"a[1][2]" /((`a;1);2)
&"a[1;2]+:3" /(+:;(`a;1;2);3)
&"x y" /(`x;`y)
&"x y z" /(`x;(`y;`z))
&"x(y;z)" /(`x;,(`y;`z))
&"x(*2)" /(`x;(*;2))
&"x" /`x
&"*x" /(*;`x)
&"x(y)" /(`x;`y)
&"x(y z)" /(`x;(`y;`z))
&"f[a]" /(`f;`a)
&"f a" /(`f;`a)
&"f[a;b]" /(`f;`a;`b)
&"f[(a;b)]" /(`f;,(`a;`b))
&"f(a;b)" /(`f;,(`a;`b))
&"f[a][b]" /((`f;`a);`b)
&"m[*i]" /(`m;(*;`i))
&"*1" /(*;1)
&"+/1" /((/;+);1)
/&"\"abc\"" /"abc"
&"`a" /,`a
&"`abc" /,`abc
/&"`\"abc\"" /,`abc
&"f[a;b]" /(`f;`a;`b)
&"(1;2)" /,(1;2)
&"(1;(2;3))" /,(1;,(2;3))
&"(a;(1;2);3)" /,(`a;,(1;2);3)
&"$[1;+/3;4]" /($;1;((/;+);3);4)
&"x+m[*i]/y" /(+;`x;((/;(`m;(*;`i)));`y))
&"a;b;c" /(;`a;`b;`c)
a:1 /1
a:1;a:2;a /2
a:(1;2;3;4);a /(1;2;3;4)
a:1;a:a+1;a /2
a:1;a+:1;a /2
a:1;b:a+:1;b /2
{x}2 /2
{a:1;x+y+a}[1;2] /4
a:3;{a:1;x+y+a}[1;2] /4
a:3;{a:1;x+y+a}[1;2];a /3
a:3+!10;a[2 3 4] /5 6 7
(1 2;3 4)@1 0 /(3 4;1 2)
(1+!10)@(2 3;4 5) /(3 4;5 6)
(`a`b`c!(1 2;3 4;5 6))[`c;1] /6
(1 2;3 4;5 6)[1;0] /3
(`a`b`c!1 2 3)1 /2
(`a`b`c!1 2 3)@`b /2
(`a`b`c!1 2 3)@`c`b /(3;2)
(1;2 3).(1;0) /2
(3 3#!9).(;1) /(1;4;7)
(`a`b!(1 2;3 4))[`b`a;1] /(4;2)
a:10+!10;a@`a`b!(1 2;3 4) /`a`b!(11 12;13 14)
a:10+!10;a[`a`b!(1 2;3 4)] /`a`b!(11 12;13 14)
a:10+!10;a`a`b!(1 2;3 4) /`a`b!(11 12;13 14)
d:`a`b!1 2;d[`b]:3.0;d /`a`b!(1;3.0)
a:!10;a[2 3 4]:0;a /0 1 0 0 0 5 6 7 8 9
a:(1;2;3;4);a[3 0]:-1 -2;a /(-2;2;3;-1)
a:2 3#!6;a[0 1;1 2]:(9 8;7 6) /(0 9 8;3 7 6)
a:2 3#!6;a[0 1;1 2]:9;a /(0 9 9;3 9 9)
x:`a`b!(1 2;3 4);x[`b;0]:-1;x /`a`b!(1 2;-1 4)
x:`a`b!(1 2;3 4);x[`b`a;1]:-1;x /`a`b!(1 -1;3 -1)
x:`a`b!(1 2;3 4);x[;1]:-1;x /`a`b!(1 -1;3 -1)
a:2 3#!6;a[1;2]:-1;a /(0 1 2;3 4 -1)
x:`a`b!1 2;x[`b]:3;x /`a`b!(1;3)
x:1 2 3;a:1;x[a]:-1;x /1 -1 3
